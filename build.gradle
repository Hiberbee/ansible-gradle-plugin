plugins {
  id "com.github.ben-manes.versions" version "0.29.0"
  id "com.gradle.plugin-publish" version "0.12.0"
  id "groovy"
  id "idea"
  id "jacoco"
  id "java-gradle-plugin"
  id "nu.studer.credentials" version "2.1"
  id "org.jetbrains.gradle.plugin.idea-ext" version "0.10"
  id "org.sonarqube" version "3.0"
}

def organization = "hiberbee"
group "com.${organization}.gradle"
version "0.4.0"

idea {
  module {
    outputDir project.buildDir
    downloadJavadoc false
    downloadSources true
    inheritOutputDirs true
    excludeDirs += files(".gradle", ".idea", ".scannerwork", "gradle")
    generatedSourceDirs += files(
      "${project.buildDir}/generated/sources/annotationProcessor/java/main",
      "${project.buildDir}/generated/sources/annotationProcessor/java/test"
    )
  }
}
sonarqube {
  properties {
    property "sonar.login", System.getenv("SONAR_TOKEN")
    property "sonar.organization", organization
    property "sonar.projectKey", project.name
    property "sonar.projectName", project.name
    property "sonar.host.url", "https://sonarcloud.io"
    property "sonar.java.binaries", "${project.buildDir}/libs"
    property "sonar.groovy.libraries", "${project.buildDir}/classes/java/main"
    property "sonar.groovy.test.libraries", "${project.buildDir}/classes/java/test"
    property "sonar.coverage.jacoco.xmlReportPaths", "${project.buildDir}/reports/jacoco/test/*.xml"
  }
}

repositories {
  mavenCentral()
  mavenLocal()
}

test {
  useJUnitPlatform()
}

gradlePlugin {
  plugins {
    ansiblePlugin {
      id = "com.hiberbee.gradle.plugin.ansible"
      implementationClass = "com.hiberbee.gradle.plugin.AnsiblePlugin"
    }
  }
}

pluginBundle {
  website = "https://github.com/hiberbee/gradle-plugin-ansible"
  vcsUrl = "https://github.com/hiberbee/gradle-plugin-ansible"
  description = "Plugin allows to create and configure Ansible tasks with Gradle"
  tags = ["ansible", "configuration-management"]
  plugins {
    ansiblePlugin {
      displayName = "Ansible Gradle plugin"
    }
  }
}

dependencies {
  compile localGroovy()
  implementation gradleApi()
  testImplementation "org.junit.jupiter:junit-jupiter:5.7.1"
  testImplementation "org.junit.jupiter:junit-jupiter-engine:5.7.0-RC1"
}

